Функция СведенияОВнешнейОбработке() Экспорт 
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке(СтандартныеПодсистемыСервер.ВерсияБиблиотеки());	
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма();
	ПараметрыРегистрации.Наименование = "Акт общей формы";
	ПараметрыРегистрации.Версия = "1.0";
	ПараметрыРегистрации.Информация = "Внешняя печатная форма АОФ для документа ""уатАктПриемки"". Задача № 2542 Профрешение";
	ПараметрыРегистрации.БезопасныйРежим = Ложь; 
	ПараметрыРегистрации.Назначение = ПолучитьНазначениеОбработки();
	ДобавитьКоманду(ПараметрыРегистрации.Команды, "Акт общей формы", "АОФ", ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода(), Ложь, "ПечатьMXL"); 
	
	Возврат ПараметрыРегистрации;
	
КонецФункции  

функция ПолучитьНазначениеОбработки() Экспорт
	
	Массив = Новый Массив;
	Массив.Добавить("Документ.уатАктПриемки_уэ");
	Возврат Массив;
	
КонецФункции    

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры 

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ТабличныйДокументПечатьАОФ = ПечатьАОФ(МассивОбъектов); 
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "АОФ", Нстр("ru = 'Печать АОФ'"), ТабличныйДокументПечатьАОФ);
	
КонецПроцедуры 

Функция ПечатьАОФ(МассивОбъектов)
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;  
	ТабДокумент.АвтоМасштаб = Истина;
	Макет = ПолучитьМакет("АОФ");
	
	Для Каждого мОбъект из МассивОбъектов Цикл 
		СсылкаНаДокумент = мОбъект.Ссылка;
		ОбластьМакетаДокумент = Макет.ПолучитьОбласть("ОбластьДокумент");
		ЗапросСходство = Новый Запрос;
		ЗапросСходство.УстановитьПараметр("ТекущийДокумент", СсылкаНаДокумент);
		ЗапросСходство.Текст =
		"ВЫБРАТЬ
		|	уатАктПриемки_уэГрузы.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.уатАктПриемки_уэ.Грузы КАК уатАктПриемки_уэГрузы
		|ГДЕ
		|	уатАктПриемки_уэГрузы.Ссылка = &ТекущийДокумент
		|	И уатАктПриемки_уэГрузы.ВесБруттоПлан <> уатАктПриемки_уэГрузы.ВесБрутто"; 
		Результат = ЗапросСходство.Выполнить();
		Если Результат.Пустой() Тогда
			ОбщегоНазначения.СообщитьПользователю("Документ № " + СсылкаНаДокумент.Номер + " от " + СсылкаНаДокумент.Дата + " не будет распечатан! Фактический вес совпадает с плановым. ");  
			Продолжить;
		КонецЕсли;
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент", СсылкаНаДокумент);
		Запрос.Текст =		
		"ВЫБРАТЬ
		|	уатАктПриемки_уэГрузы.ГрузовоеМесто КАК Номенклатура,
		|	СУММА(ЕСТЬNULL(уатАктПриемки_уэГрузы.ВесБрутто, 0)) КАК ВесБрутто,
		|	СУММА(ЕСТЬNULL(уатАктПриемки_уэГрузы.ВесБруттоПлан, 0)) КАК ВесБруттоПлан,
		|	СУММА(ЕСТЬNULL(уатАктПриемки_уэГрузы.Количество, 0)) КАК Количество,
		|	СУММА(ЕСТЬNULL(уатАктПриемки_уэГрузы.КоличествоПлан, 0)) КАК КоличествоПлан,
		|	МАКСИМУМ(уатАктПриемки_уэ.ПР_Контрагент.Наименование) КАК КонтрагентНаименование,
		|	МАКСИМУМ(уатАктПриемки_уэ.Организация.Наименование) КАК ОрганизацияНаименование,
		|	МАКСИМУМ(уатАктПриемки_уэ.Склад.Наименование) КАК Склад,
		|	МАКСИМУМ(уатАктПриемки_уэ.Номер) КАК НомерДокумента,
		|	МАКСИМУМ(уатАктПриемки_уэ.Дата) КАК ДатаДокумента
		|ИЗ
		|	Документ.уатАктПриемки_уэ КАК уатАктПриемки_уэ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.уатАктПриемки_уэ.Грузы КАК уатАктПриемки_уэГрузы
		|		ПО уатАктПриемки_уэ.Ссылка = уатАктПриемки_уэГрузы.Ссылка
		|ГДЕ
		|	уатАктПриемки_уэ.Ссылка = &ТекущийДокумент
		|
		|СГРУППИРОВАТЬ ПО
		|	уатАктПриемки_уэГрузы.ГрузовоеМесто
		|
		|ИМЕЮЩИЕ
		|	СУММА(ЕСТЬNULL(уатАктПриемки_уэГрузы.ВесБрутто, 0)) <> СУММА(ЕСТЬNULL(уатАктПриемки_уэГрузы.ВесБруттоПлан, 0))";		
		
		Выборка = Запрос.Выполнить().Выбрать(); 
		
		Пока Выборка.Следующий() Цикл
			
			ОбластьМакетаДокумент.Параметры.Заполнить(Выборка);
			ТабДокумент.Вывести(ОбластьМакетаДокумент);
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		КонецЦикла; 
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции