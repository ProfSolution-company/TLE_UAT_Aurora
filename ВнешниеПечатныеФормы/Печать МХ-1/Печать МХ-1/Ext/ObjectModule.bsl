#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт 
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке(СтандартныеПодсистемыСервер.ВерсияБиблиотеки());	
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма();
	ПараметрыРегистрации.Наименование = "Печать МХ-1";
	ПараметрыРегистрации.Версия = "1.0";
	ПараметрыРегистрации.Информация = "Внешняя печатная форма МХ-1 для документа ""АктПриемки"". Задача № 2541 Профрешение";
	ПараметрыРегистрации.БезопасныйРежим = Ложь; 
	ПараметрыРегистрации.Назначение = ПолучитьНазначениеОбработки();
	ДобавитьКоманду(ПараметрыРегистрации.Команды, "Печать МХ-1", "МХ1", ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода(), Ложь, "ПечатьMXL"); 
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

функция ПолучитьНазначениеОбработки() Экспорт
	
	Массив = Новый Массив;
	Массив.Добавить("Документ.уатАктПриемки_уэ");
	Возврат Массив;
	
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;
	НоваяКоманда.Использование = Использование;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры 

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ТабличныйДокументПечатьМХ1 = ПечатьМХ1(МассивОбъектов); 
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МХ1", Нстр("ru = 'Печать МХ-1'"), ТабличныйДокументПечатьМХ1);
	
КонецПроцедуры  

#КонецОбласти 

#Область ВыводДанныхМХ1

Функция ПечатьМХ1(МассивОбъектов)
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;  
	ТабДокумент.АвтоМасштаб = Истина;
	Макет = ПолучитьМакет("МХ1");
	
	Для Каждого мОбъект из МассивОбъектов Цикл 
		СсылкаНаДокумент = мОбъект.Ссылка;
		
		ПечатьШапки(ТабДокумент, Макет, СсылкаНаДокумент);
		
		ПечатьСтрок(ТабДокумент, Макет, СсылкаНаДокумент);
		
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции 

Процедура ПечатьШапки(ТабДокумент, Макет, СсылкаНаДокумент) 
	
	ЗапросШапка = Новый Запрос;
	ЗапросШапка.УстановитьПараметр("ТекущийДокумент", СсылкаНаДокумент);
	ЗапросШапка.Текст = 
	"ВЫБРАТЬ
	|	уатАктПриемки_уэ.Номер КАК НомерДокумента,
	|	уатАктПриемки_уэ.Дата КАК ДатаДокумента,
	|	уатАктПриемки_уэ.ПР_Контрагент КАК Контрагент,
	|	уатАктПриемки_уэ.Склад КАК Склад,
	|	уатАктПриемки_уэ.Организация КАК Организация,
	|	Организации.Наименование КАК НаименованиеОрганизации,
	|	Организации.КодПоОКПО КАК ОКПООрганизации,
	|	ДоговорыКонтрагентов.Номер КАК ДоговорНомер,
	|	ДоговорыКонтрагентов.Дата КАК ДоговорДата,
	|	Контрагенты.КодПоОКПО КАК ОКПОПокупателя,
	|	Контрагенты.Наименование КАК НаименованиеКонтрагента
	|ИЗ
	|	Документ.уатАктПриемки_уэ КАК уатАктПриемки_уэ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО уатАктПриемки_уэ.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО уатАктПриемки_уэ.ПР_Договор = ДоговорыКонтрагентов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО уатАктПриемки_уэ.ПР_Контрагент = Контрагенты.Ссылка
	|ГДЕ
	|	уатАктПриемки_уэ.Ссылка = &ТекущийДокумент";   
	
	Шапка = ЗапросШапка.Выполнить().Выбрать(); 
	Шапка.Следующий();  
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	
	ПредставлениеПодразделения = Шапка.Склад;
	ПредставлениеОрганизации = Шапка.НаименованиеОрганизации;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект", Шапка.Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КонтактнаяИнформация.Представление КАК Представление
	|ИЗ
	|	Справочник.Организации.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Ссылка = &Объект";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ПредставлениеОрганизации = ПредставлениеОрганизации + Выборка.Представление;
	КонецЕсли; 
	
	ПредставлениеПокупателя = Шапка.НаименованиеКонтрагента;
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект", Шапка.Контрагент); 
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КонтактнаяИнформация.Представление КАК Представление
	|ИЗ
	|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтактнаяИнформация
	|ГДЕ
	|	КонтактнаяИнформация.Ссылка = &Объект";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ПредставлениеПокупателя = ПредставлениеПокупателя + Выборка.Представление;
	КонецЕсли;
	
	ОбластьМакета.Параметры.ПредставлениеПодразделения = ПредставлениеПодразделения;
	ОбластьМакета.Параметры.ПредставлениеОрганизации = ПредставлениеОрганизации;
	ОбластьМакета.Параметры.ПредставлениеПокупателя = ПредставлениеПокупателя;
	ОбластьМакета.Параметры.Заполнить(Шапка);
	ТабДокумент.Вывести(ОбластьМакета); 
	
КонецПроцедуры 

Процедура ПечатьСтрок(ТабДокумент, Макет, СсылкаНаДокумент)
	
	ЗапросСтрока = Новый Запрос; 
	ЗапросСтрока.УстановитьПараметр("ТекущийДокумент", СсылкаНаДокумент);
	ЗапросСтрока.Текст = 
	"ВЫБРАТЬ
	|	уатАктПриемки_уэГрузы.ГрузовоеМесто КАК Наименование,
	|	уатАктПриемки_уэГрузы.ПР_ВесНетто КАК ВесНетто,
	|	уатАктПриемки_уэГрузы.ВесБрутто КАК ВесБрутто,
	|	уатАктПриемки_уэГрузы.ЕдиницаИзмерения КАК ЕдиницыИзмеренияНаименование,
	|	уатАктПриемки_уэГрузы.Количество КАК КоличествоВОсновнойУпаковке,
	|	уатАктПриемки_уэГрузы.Количество КАК КоличествоМест,
	|	уатАктПриемки_уэГрузы.Количество КАК Количество,
	|	Номенклатура.Артикул КАК КодТовара,
	|	Номенклатура.уатВидУпаковкиОсновной КАК ВидУпаковкиНаименование,
	|	Номенклатура.НаименованиеПолное КАК ПолноеНаименование,
	|	уатВидыУпаковки_уэ.Коэффициент КАК Коэффициент
	|ИЗ
	|	Документ.уатАктПриемки_уэ.Грузы КАК уатАктПриемки_уэГрузы
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО уатАктПриемки_уэГрузы.ГрузовоеМесто = Номенклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уатВидыУпаковки_уэ КАК уатВидыУпаковки_уэ
	|		ПО уатАктПриемки_уэГрузы.ЕдиницаИзмерения = уатВидыУпаковки_уэ.Ссылка
	|ГДЕ
	|	уатАктПриемки_уэГрузы.Ссылка = &ТекущийДокумент";
	Строки = ЗапросСтрока.Выполнить().Выбрать();
	
	ОбластьМакетаСтроки = Макет.ПолучитьОбласть("Строка");
	ОбластьМакетаВсего = Макет.ПолучитьОбласть("Всего");
	ОбластьМакетаИтого = Макет.ПолучитьОбласть("Итого");
	ОбластьМакетаЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ТабДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);
	
	ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
	
	КоличествоСтрок = Строки.Количество();
	
	НомерПоПорядку = 1;
	НомерСтроки = 1;
	
	ИтогоВес = 0;
	ВсегоВес = 0;
	
	ИтогоУпаковка = 0;
	ВсегоУпаковка = 0;   
	
	ИтогоНетто = 0;
	ВсегоНетто = 0;
	
	ИтогоБрутто = 0;
	ВсегоБрутто = 0;
	
	Пока Строки.Следующий() Цикл
		СтрокаСПодвалом = Новый Массив;
		Если НомерСтроки = 1 Тогда
			СтрокаСПодвалом.Добавить(ОбластьМакетаЗаголовокТаблицы); 
		КонецЕсли;
		СтрокаСПодвалом.Добавить(ОбластьМакетаСтроки);
		СтрокаСПодвалом.Добавить(ОбластьМакетаИтого);
		Если НомерСтроки = КоличествоСтрок Тогда           
			СтрокаСПодвалом.Добавить(ОбластьМакетаВсего);  
			СтрокаСПодвалом.Добавить(ОбластьМакетаПодвал);
		КонецЕсли;
		
		Если НЕ ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабДокумент, СтрокаСПодвалом) Тогда
			
			Если КоличествоСтрок > 1 Тогда
				
				ОбластьМакетаИтого.Параметры.ИтогоУпаковка       = ИтогоУпаковка;
				ОбластьМакетаИтого.Параметры.ИтогоВес = ИтогоВес;
				ОбластьМакетаИтого.Параметры.ИтогоНетто = ИтогоНетто; 
				ОбластьМакетаИтого.Параметры.ИтогоБрутто = ИтогоБрутто;
				ТабДокумент.Вывести(ОбластьМакетаИтого);
				
				ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				
				ИтогоУпаковка = 0;
				ИтогоВес = 0; 
				ИтогоНетто = 0;
				ИтогоБрутто = 0;
				ТабДокумент.Вывести(ОбластьМакетаЗаголовокТаблицы);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ОбластьМакетаСтроки.Параметры.НомерПоПорядку = НомерПоПорядку;
		
		ОбластьМакетаСтроки.Параметры.Заполнить(Строки);
		ТабДокумент.Вывести(ОбластьМакетаСтроки);
		НомерПоПорядку = НомерПоПорядку + 1;
		
		ВсегоУпаковка =   ВсегоУпаковка +  Строки.Количество; 
		ИтогоУпаковка =   ИтогоУпаковка +  Строки.Количество;
		
		ВсегоНетто =   ВсегоНетто +  Строки.ВесНетто; 
		ИтогоНетто =   ИтогоНетто +  Строки.ВесНетто;
		
		ВсегоБрутто =   ВсегоБрутто +  Строки.ВесБрутто; 
		ИтогоБрутто =   ИтогоБрутто +  Строки.ВесБрутто;
		
		ВсегоВес =   ВсегоВес +  Строки.Количество; 
		ИтогоВес =   ИтогоВеС +  Строки.Количество;
		
		ОбластьМакетаВсего.Параметры.ВсегоУпаковка = ВсегоУпаковка;
		ОбластьМакетаИтого.Параметры.ИтогоУпаковка = ИтогоУпаковка; 
		
		ОбластьМакетаВсего.Параметры.ВсегоНетто = ВсегоНетто;
		ОбластьМакетаИтого.Параметры.ИтогоНетто = ИтогоНетто;
		
		ОбластьМакетаВсего.Параметры.ВсегоБрутто = ВсегоБрутто;
		ОбластьМакетаИтого.Параметры.ИтогоБрутто = ИтогоБрутто;
		
		ОбластьМакетаВсего.Параметры.ВсегоВес = ВсегоВес;
		ОбластьМакетаИтого.Параметры.ИтогоВеС = ИтогоВеС;
		
	КонецЦикла;
	ТабДокумент.Вывести(ОбластьМакетаВсего);
	ТабДокумент.Вывести(ОбластьМакетаИтого); 
	ПечатьПодвала(ТабДокумент, Макет, СсылкаНаДокумент);
	
КонецПроцедуры

Процедура ПечатьПодвала(ТабДокумент, Макет, СсылкаНаДокумент)
	
	ЗапросПодвал = Новый Запрос;
	ЗапросПодвал.УстановитьПараметр("ТекущийДокумент", СсылкаНаДокумент);
	ЗапросПодвал.Текст = 
	"ВЫБРАТЬ
	|	СотрудникСдал.Должность КАК ДолжностьСдал,
	|	СотрудникПринял.Должность КАК ДолжностьПринял,
	|	уатАктПриемки_уэ.Отпустил КАК РасшифровкаПодписиСдал,
	|	уатАктПриемки_уэ.Получил КАК РасшифровкаПодписиПолучил,
	|	уатАктПриемки_уэ.ПР_ОсобыеОтметки КАК ПР_ОсобыеОтметки
	|ИЗ
	|	Документ.уатАктПриемки_уэ КАК уатАктПриемки_уэ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК СотрудникСдал
	|		ПО уатАктПриемки_уэ.Отпустил = СотрудникСдал.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК СотрудникПринял
	|		ПО уатАктПриемки_уэ.Получил = СотрудникПринял.Ссылка
	|ГДЕ
	|	уатАктПриемки_уэ.Ссылка = &ТекущийДокумент";
	
	Подвал = ЗапросПодвал.Выполнить().Выбрать(); 
	
	Если Подвал.Следующий() Тогда
		
		ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
		ОбластьМакета.Параметры.Заполнить(Подвал);
		ТабДокумент.Вывести(ОбластьМакета);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти



